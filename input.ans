
H2 Input

MD(
The first thing an interpreter must be able to do is to take in the user's code
and data inputI{input}, whether from the keyboard or from a source file.  K{read}
represents the interpreter's main input facility. The K{read-} procedures read
from standard input, or--if a port is provided--from the port's channel.
MD)

H3 Parsing

H4 The parsing process

MD(
W{Parsing}{Parsing}I{parsing}, or syntactic analysis, is analyzing a sequence of
letters, digits, and other characters, conforming to the rules of E{external
  representation}I{external representation}. The result of parsing is an
  E{expression}I{expression} in internal form.

The parsing process translates an expression from external representation to
internal representation. The external representation is a 'recipe' for an
expression that expresses it in a unique way. 

For example, the external representation for a vector is a sharp sign (K{#}), a
left parenthesis (K{(}), the external representation for some values, and a right
parenthesis (K{)}). When the reader or parser is working through input, a K{#(}
symbol signals that a vector structure is being read. A number of subexpressions
for the elements of the vector follow, and then a closing parenthesis K{)}
signals that the vector is done. The elements are saved in vector memory and the
vector gets the address to the first element and the number of elements.

IG /images/vector-representation.png

The K{parse} procedure takes in the input buffer character by character, matching
each character against a fitting external representation. When done, it creates
a ConsTcl object, which is the internal representation of an expression.  The
object can then be passed to the evaluator.

Given a string, K{parse} fills the input buffer. It then parses the input and
produces the internal representation of an expression.

Example:
MD)

VB(
% ::constcl::parse "(+ 2 3)"
::oo::Obj491
VB)

MD(
Here, K{parse} parsed the external representation of a list with three elements,
+, 2, and 3. It produced the expression that has an internal representation
labeled K{::oo::Obj491}. We will later meet procedures like K{eval}, which
transforms an expression into a value, and K{write}, which prints a printed
representation of expressions and values. Putting them together: we can see
MD)

VB(
% ::constcl::write ::oo::Obj491
(+ 2 3)
% ::constcl::eval ::oo::Obj491
::oo::Obj494
% ::constcl::write ::oo::Obj494
5
VB)

MD(
Fortunately, we don't have to work at such a low level. We can use the
K{repl}I{repl} instead:
MD)

VB(
ConsTcl> (+ 2 3)
5
VB)

MD(
Then, parsing and evaluation and writing goes on in the background and the
internal representations of expressions and values are hidden.

Anyway, here is how it really looks like. K{::oo::Obj491} was just the head of
the list.

IG /images/intreplist.png

B{parse} procedure

K{parse} can be called with either a string input port or a Tcl or ConsTcl
string (which K{parse} uses to open a string input port). Once the input port is
established, K{parse} leaves control to K{read-expr}.
MD)

PR(
parse (internal);inp tstrstrinpbuf -> expr
PR)

IX parse
CB(
reg parse

proc ::constcl::parse {inp} {
  set c {}
  set unget {}
  if {[info object isa object $inp]} {
    if {[typeof? $inp StringInputPort] ne "#f"} {
      set port $inp
    } elseif {[typeof? $inp String] ne "#f"} {
      set port [StringInputPort new [$inp value]]
    } else {
      ::error "Unknown object [$inp show]"
    }
  } else {
    # It's a Tcl string, we hope
    set port [StringInputPort new $inp]
  }
  set oldport $::constcl::Input_port
  set ::constcl::Input_port $port
  set expr [read-expr]
  set ::constcl::Input_port $oldport
  return $expr
}
CB)

TT(
::tcltest::test read-1.0 {try parse in ConsTcl} -body {
  pew {(parse "42")}
} -output "42\n"

::tcltest::test read-1.1 {try parse with StringInputPort buffer} -body {
  pw [::constcl::StringInputPort new "42"]
} -output "42\n"

::tcltest::test read-1.2 {try parse with string} -body {
  pw "42"
} -output "42\n"
TT)

MD(
B{make-constant} procedure

The K{make-constant} helper procedure is called to set expressions to constants
when read as a literal.
MD)

IX make-constant
CB(
proc ::constcl::make-constant {val} {
  if {[pair? $val] ne "#f"} {
    $val mkconstant
    make-constant [car $val]
    make-constant [cdr $val]
  } elseif {[null? $val] ne "#f"} {
    return #NIL
  } else {
    $val mkconstant
  }
}
CB)

MD(
B{interspace} procedure

The K{interspace} helper procedure recognizes whitespace between value
representations.
MD)

IX interspace
CB(
proc ::constcl::interspace {c} {
  if {[::string is space $c] || $c eq ";"} {
      return #t
    } else {
      return #f
    }
}
CB)

MD(
B{character-check} procedure

The K{character-check} helper procedure compares a potential
character constant to the valid kinds.
MD)

PR(
character-check (internal);name tstr -> bool
PR)

IX character-check
CB(
proc ::constcl::character-check {name} {
  if {[regexp {(?i)^#\\([[:graph:]]|space|newline)$} \
      $name]} {
    return #t
  } else {
    return #f
  }
}
CB)

H3 read

MD(
B{read} procedure

The standard builtin K{read} reads an input port the same way that K{parse}
takes in an input buffer. The K{read-} procedures parse their input and produce
ConsTcl objects.

One can pass a port to K{read}, in which case K{read} sets the standard input
port temporarily to the provided port. If not, K{read} uses the default standard
input port (usually the keyboard).
MD)

PR(
read (public);?port? port -> expr
PR)

IX read
CB(
reg read

proc ::constcl::read {args} {
  set c {}
  set unget {}
  if {[llength $args]} {
    lassign $args port
  } else {
    set port $::constcl::Input_port
  }
  set oldport $::constcl::Input_port
  set ::constcl::Input_port $port
  set expr [read-expr]
  set ::constcl::Input_port $oldport
  return $expr
}
CB)

MD(
B{read-expr} procedure

The procedure K{read-expr} reads input by reading the first available
character and delegating to one of the more detailed reading procedures based on
that, producing an expression of any kind. A Tcl character value can be passed
to it, that character will be used first before reading from the input stream.
If end of file is encountered before an expression can be read in full, the
procedure returns end of file (K{#EOF})I{end of file}.
MD)

PR(
read-expr (internal);?char? tchr -> expreof
PR)

IX read-expr
CB(
proc ::constcl::read-expr {args} {
  upvar c c unget unget
  if {[llength $args]} {
    lassign $args c
  } else {
    set c [readc]
  }
  set unget {}
  read-eof $c
  if {[::string is space $c] || $c eq ";"} {
    skip-ws
    read-eof $c
  }
  switch -regexp $c {
    {\"}          { read-string-expr }
    {\#}          { read-sharp }
    {\'}          { read-quoted-expr }
    {\(}          { read-pair-expr ")" }
    {\+} - {\-}   { read-plus-minus $c }
    {\,}          { read-unquoted-expr }
    {\.} {
        set x [Dot new]; set c [readc]; set x
    }
    {\:}          { read-object-expr }
    {\[}          { read-pair-expr "\]" }
    {\`}          { read-quasiquoted-expr }
    {\d}          { read-number-expr $c }
    {^$}          { return}
    {[[:graph:]]} { read-identifier-expr $c }
    default {
      read-eof $c
      ::error "unexpected character ($c)"
    }
  }
}
CB)

TT(
::tcltest::test read-2.0 {try read-expr on dot expr} -setup {
    ::tcltest::makeFile {  a . b } testrr.lsp
    set p [pe {(open-input-file "testrr.lsp")}]
} -body {
    rw $p
    rw $p
    rw $p
} -cleanup {
    ::constcl::close-input-port $p
    ::tcltest::removeFile testrr.lsp
} -output "a\n.\nb\n"

TT)

MD(
B{readc} procedure

K{readc} reads one character from the unget store if it isn't empty or else from
the input stream. If the input stream is at end-of-file, an eof object is
returned.
MD)

PR(
readc (internal);-> tchreof
PR)

IX readc
CB(
proc ::constcl::readc {} {
  upvar unget unget
  if {$unget ne {}} {
    set c $unget
    set unget {}
  } else {
    set c [$::constcl::Input_port get]
    if {[$::constcl::Input_port eof]} {
      return #EOF
    }
  }
  return $c
}
CB)

TT(
# 3
TT)

MD(
B{read-find} procedure

K{read-find} reads ahead through whitespace to find a given character. Returns 1
if it has found the character, and 0 if it has stopped at some other character.
Returns end of file if eof is encountered.
MD)

PR(
read-find (internal);char tchr -> tbooleof
PR)

IX read-find
CB(
proc ::constcl::read-find {char} {
  upvar c c unget unget
  while {[::string is space -strict $c]} {
    set c [readc]
    read-eof $c
    set unget $c
  }
  return [expr {$c eq $char}]
}
CB)

TT(
# 4
TT)

MD(
B{read-end} procedure

K{read-end} reads one character and returns 1 if it is an interspace character or
an ending parenthesis or bracket, or end of file. Otherwise it returns 0. It
ungets the character before returning.
MD)

PR(
read-end (internal);-> tbooleof
PR)

IX read-end
CB(
proc ::constcl::read-end {} {
  upvar c c unget unget
  set c [readc]
  if {[interspace $c] ne "#f" || $c in {) ]} || $c eq "#EOF"} {
    set unget $c
    return 1
  } else {
    set unget $c
    return 0
  }
}
CB)

TT(
# 5
TT)

MD(
B{skip-ws} procedure

K{skip-ws} skips whitespace and comments (the K{;} to end of line kind). Uses the
shared E{c} variable. It leaves the first character not to be skipped in E{c}.
MD)

PR(
skip-ws (internal);-> none
PR)

IX skip-ws
CB(
proc ::constcl::skip-ws {} {
  upvar c c unget unget
  while true {
    switch -regexp $c {
      {[[:space:]]} {
        set c [readc]
      }
      {;} {
        while {$c ne "\n" && $c ne "#EOF"}  {
          set c [readc]
        }
      }
      default {
        set unget $c
        return
      }
    }
  }
}
CB)

TT(
# 6
TT)

MD(
B{read-eof} procedure

K{read-eof} checks a number of presumed characters for possible end-of-file
objects. If it finds one, it returns E{from its caller} with the EOF value.
MD)

PR(
read-eof (internal);chars chars
PR)

IX read-eof
CB(
proc ::constcl::read-eof {args} {
  set chars $args
  foreach char $chars {
    if {$char eq "#EOF"} {
      return -level 1 -code return #EOF
    }
  }
}
CB)

TT(
# 7
TT)

MD(
B{read-string-expr} procedure

K{read-string-expr} is activated by K{read-expr} when it reads a double quote. It
collects characters until it reaches another (unescaped) double quote. It then
returns a string expression--an immutable R{String}{strings} object.
MD)

PR(
read-string-expr (internal);-> streof
PR)

IX read-string-expr
CB(
proc ::constcl::read-string-expr {} {
  upvar c c unget unget
  set str {}
  set c [readc]
  read-eof $c
  while {$c ne "\"" && $c ne "#EOF"} {
    if {$c eq "\\"} {
      ::append str $c
      set c [readc]
    }
    ::append str $c
    set c [readc]
  }
  if {$c eq "#EOF"} {
    error "bad string (no ending double quote)"
  }
  set c [readc]
  set expr [MkString $str]
  read-eof $expr
  $expr mkconstant
  return $expr
}
CB)

TT(

::tcltest::test read-8.0 {try reading a string} {
    set expr [p {"foo bar"}]
    $expr value
} "foo bar"

::tcltest::test read-8.1 {try reading a string} {
    set expr [p {"\"foo\" \\ bar"}]
    $expr value
} {"foo" \ bar}

::tcltest::test read-8.2 {try reading a string} {
    set expr [p {"\"foo\" \\ bar"}]
    $expr external
} {"\"foo\" \\ bar"}

::tcltest::test read-8.3 {try read-expr on a string} -setup {
    ::tcltest::makeFile {"foo bar"  } testrr.lsp
    set p [pe {(open-input-file "testrr.lsp")}]
} -body {
    rw $p
} -cleanup {
    ::constcl::close-input-port $p
    ::tcltest::removeFile testrr.lsp
} -output "\"foo bar\"\n"

::tcltest::test read-8.4 {try read-expr on a string/eof} -setup {
    ::tcltest::makeFile {"foo } testrr.lsp ; #"
    set p [pe {(open-input-file "testrr.lsp")}]
} -body {
    rw $p
} -cleanup {
    ::constcl::close-input-port $p
    ::tcltest::removeFile testrr.lsp
} -returnCodes error -result {bad string (no ending double quote)}

TT)

MD(
B{read-sharp} procedure

K{read-sharp} is activated by K{read-expr} when it reads a sharp sign (K{#}). It
in turn either delegates to the vector reader or the character reader, or
returns boolean literals.
MD)

PR(
read-sharp (internal);-> sharpeof
PR)

IX read-sharp
CB(
proc ::constcl::read-sharp {} {
  upvar c c unget unget
  set unget {}
  set c [readc]
  read-eof $c
  switch $c {
    (    { set n [read-vector-expr] }
    t    { if {[read-end]} {set n #t} }
    f    { if {[read-end]} {set n #f} }
    "\\" { set n [read-character-expr] }
    default {
      ::error "Illegal #-literal: #$c"
    }
  }
  return $n
}
CB)

TT(

::tcltest::test read-9.0 {try read-expr on booleans} -setup {
    ::tcltest::makeFile {  #t  #f} testrr.lsp
    set p [pe {(open-input-file "testrr.lsp")}]
} -body {
    rw $p
    rw $p
} -cleanup {
    ::constcl::close-input-port $p
    ::tcltest::removeFile testrr.lsp
} -output "#t\n#f\n"

TT)

MD(
B{read-vector-expr} procedure

K{read-vector-expr} is activated by K{read-sharp} and reads a number of
expressions until it finds an ending parenthesis.  It produces a vector
expression and returns a R{Vector}{vectors} object.
MD)

PR(
read-vector-expr (internal);-> veceof
PR)

IX read-vector-expr
CB(
proc ::constcl::read-vector-expr {} {
  upvar c c unget unget
  set res {}
  set last {}
  set c [readc]
  while {$c ne "#EOF" && $c ne ")"} {
    set e [cons [read-expr $c] #NIL]
    if {$res eq {}} {
      set res $e
      set last $e
    } else {
      set-cdr! $last $e
      set last $e
    }
    skip-ws
    read-eof $c
  }
  if {$c ne ")"} {
    ::error "Missing right paren. ($c)."
  }
  set expr [MkVector $res]
  read-eof $expr
  $expr mkconstant
  return $expr
}
CB)

TT(

::tcltest::test read-10.0 {try reading a vector} -body {
    pew "#(1 2 3)"
} -output "#(1 2 3)\n"

::tcltest::test read-10.1 {try reading a vector, with non-normal expression} -body {
    pew "#(1 2 (+ 1 2))"
} -output "#(1 2 (+ 1 2))\n"

::tcltest::test read-10.2 {try read-expr on a couple of vectors} -setup {
    ::tcltest::makeFile {  #(1 2 3)  #(11 22 33)} testrr.lsp
    set p [pe {(open-input-file "testrr.lsp")}]
} -body {
    rw $p
    rw $p
} -cleanup {
    ::constcl::close-input-port $p
    ::tcltest::removeFile testrr.lsp
} -output "#(1 2 3)\n#(11 22 33)\n"

TT)

MD(
B{read-character-expr} procedure

K{read-character-expr} reads input, producing a character and returning
a R{Char}{characters} object.
MD)

PR(
read-character-expr (internal);-> chareof
PR)

IX read-character-expr
CB(
proc ::constcl::read-character-expr {} {
  upvar c c unget unget
  set name "#\\"
  set c [readc]
  read-eof $c
  while {$c ni {) ]} && [::string is graph $c] && $c ne "#EOF"} {
    ::append name $c
    set c [readc]
  }
  check {character-check $name} {
      Invalid character constant $name
  }
  set expr [MkChar $name]
  read-eof $expr
  return $expr
}
CB)

TT(

::tcltest::test read-11.0 {try reading a character} {
    set expr [p {#\A}]
    $expr char
} "A"

::tcltest::test read-11.1 {try reading a character} {
    set expr [p "#\\space"]
    $expr char
} " "

::tcltest::test read-11.2 {try reading a character} {
    set expr [p "#\\newline"]
    $expr char
} "\n"

::tcltest::test read-11.3 {try reading a character} -body {
    set expr [p "#\\foobar"]
    $expr char
} -returnCodes error -result "Invalid character constant #\\foobar"

::tcltest::test read-11.4 {try read-expr on characters} -setup {
    ::tcltest::makeFile {  #\A  #\space} testrr.lsp
    set p [pe {(open-input-file "testrr.lsp")}]
} -body {
    rw $p
    rw $p
} -cleanup {
    ::constcl::close-input-port $p
    ::tcltest::removeFile testrr.lsp
} -output "#\\A\n#\\space\n"

TT)

MD(
B{read-quoted-expr} procedure

K{read-quoted-expr} is activated by K{read-expr} when reading a single quote (').
It then reads an entire expression beyond that, returning it wrapped in a list
with K{quote}. The quoted expression is made constant.
MD)

PR(
read-quoted-expr (internal);-> quoteeof
PR)

IX read-quoted-expr
CB(
proc ::constcl::read-quoted-expr {} {
  upvar c c unget unget
  set unget {}
  set expr [read-expr]
  read-eof $expr
  make-constant $expr
  return [list [S quote] $expr]
}
CB)

TT(

::tcltest::test read-12.0 {try reading quoted symbol} -body {
    pw "'foo"
} -output "(quote foo)\n"

::tcltest::test read-12.1 {try modifying quoted list} -body {
    pew "(define a '(a b c))"
    pew "(list-set! a 1 'x)"
} -returnCodes error -result "Can't modify a constant pair"

::tcltest::test read-12.2 {try read-expr on quoted expr} -setup {
    ::tcltest::makeFile {  'foo } testrr.lsp
    set p [pe {(open-input-file "testrr.lsp")}]
} -body {
    rw $p
} -cleanup {
    ::constcl::close-input-port $p
    ::tcltest::removeFile testrr.lsp
} -output "(quote foo)\n"

TT)

MD(
B{read-pair-expr} procedure

The K{read-pair-expr} procedure reads everything between two matching
parentheses, or, as the case might be, brackets. It produces either an empty
list, or a possibly recursive structure of R{Pair}{pairs-and-lists} objects,
either a proper list (one that ends in K{#NIL}), or an improper one (one that
has an atom as its last member).
MD)

PR(
read-pair-expr (internal);char pterm -> pstreof
PR)

IX read-pair-expr
CB(
proc ::constcl::read-pair-expr {char} {
  upvar c c unget unget
  set unget {}
  set expr [read-pair $char]
  read-eof $expr
  if {$c ne $char} {
    if {$char eq ")"} {
      ::error \
        "Missing right paren. ($c)."
    } else {
      ::error \
        "Missing right bracket ($c)."
    }
  } else {
    set unget {}
    set c [readc]
  }
  return $expr
}
CB)

MD(
K{read-pair} is a helper procedure that does the heavy lifting in reading a pair
structure. First it checks if the list is empty, returning K{#NIL} in that case.
Otherwise it reads the first element in the list and then repeatedly the rest of
them. If it reads a Dot object, the following element to be read is the tail
end of an improper list. When K{read-pair} has reached the ending parenthesis or
bracket, it conses up the elements starting from the last, and returns the head
of the list.
MD)

IX read-pair
CB(
proc ::constcl::read-pair {char} {
  upvar c c unget unget
  set c [readc]
  read-eof $c
  if {[read-find $char]} {
    # read right paren/brack
    #set c [readc]
    return #NIL
  }
  set a [read-expr $c]
  set res $a
  skip-ws
  set prev #NIL
  while {![read-find $char]} {
    set x [read-expr $c]
    skip-ws
    read-eof $c
    if {[dot? $x] ne "#f"} {
      set prev [read-expr $c]
      skip-ws
      read-eof $c
    } else {
      lappend res $x
    }
    if {[llength $res] > 99} break
  }
  # read right paren/brack
  foreach r [lreverse $res] {
    set prev [cons $r $prev]
  }
  return $prev
}
CB)

TT(
::tcltest::test read-13.0 {try reading an improper list} -body {
    pw "(a . b)"
} -output "(a . b)\n"

::tcltest::test read-13.1 {try reading an improper list} -body {
    pw "(a b . c)"
} -output "(a b . c)\n"

::tcltest::test read-13.2 {try reading a list} -body {
    set expr [p "(a (b))"]
    [::constcl::caadr $expr] name
} -result "b"

::tcltest::test read-13.3 {try reading a list} -body {
    pw "(a)"
} -output "(a)\n"

::tcltest::test read-13.4 {try reading a list} -body {
    pw "(a b)"
} -output "(a b)\n"

::tcltest::test read-13.5 {try reading a list} -body {
    pw "(a (b) )"
} -output "(a (b))\n"

::tcltest::test read-13.6 {try reading a list} -body {
    pw "(a (b))"
} -output "(a (b))\n"

::tcltest::test read-13.7 {try read-expr on pair expr} -setup {
    ::tcltest::makeFile {  (a b c)  ((a b) c)} testrr.lsp
    set p [pe {(open-input-file "testrr.lsp")}]
} -body {
    rw $p
} -cleanup {
    ::constcl::close-input-port $p
    ::tcltest::removeFile testrr.lsp
} -output "(a b c)\n"

::tcltest::test read-13.8 {try read-expr on pair expr} -setup {
    ::tcltest::makeFile {  ([d e] f)} testrr.lsp
    set p [pe {(open-input-file "testrr.lsp")}]
} -body {
    rw $p
} -cleanup {
    ::constcl::close-input-port $p
    ::tcltest::removeFile testrr.lsp
} -output "((d e) f)\n"

::tcltest::test read-13.9 {try read-expr on pair expr} -setup {
    ::tcltest::makeFile {  (def ghi (jkl mno))} testrr.lsp
    set p [pe {(open-input-file "testrr.lsp")}]
} -body {
    rw $p
} -cleanup {
    ::constcl::close-input-port $p
    ::tcltest::removeFile testrr.lsp
} -output "(def ghi (jkl mno))\n"

TT)

MD(
B{read-plus-minus} procedure

K{read-plus-minus} is called when a plus or minus is found in the input stream.
If the next character is a digit, it delegates to the number reader. Otherwise,
it returns a K{+} or K{-} symbol.
MD)

PR(
read-plus-minus (internal);-> pmeof
PR)

IX read-plus-minus
CB(
proc ::constcl::read-plus-minus {char} {
  upvar c c unget unget
  set unget {}
  set c [readc]
  read-eof $c
  if {[::string is digit -strict $c]} {
    set n [read-number-expr $c]
    read-eof $n
    if {$char eq "-"} {
      set n [- $n]
    }
    return $n
  } elseif {[::string is space -strict $c] ||
      $c in {) ]}} {
    if {$char eq "+"} {
      return [S "+"]
    } else {
      return [S "-"]
    }
  } else {
    set n [read-identifier-expr $char $c]
    read-eof $n
    return $n
  }
}
CB)

TT(
::tcltest::test read-14.0 {try reading a number} {
    set obj [::constcl::parse "     - "]
    $obj name
} "-"

::tcltest::test read-14.1 {try reading a number} {
    set obj [::constcl::parse "     + "]
    $obj name
} "+"
TT)

::tcltest::test read-14.2 {try read-expr on plus/minus} -setup {
    ::tcltest::makeFile {  +  -  -99} testrr.lsp
    set p [pe {(open-input-file "testrr.lsp")}]
} -body {
    rw $p
    rw $p
    rw $p
} -cleanup {
    ::constcl::close-input-port $p
    ::tcltest::removeFile testrr.lsp
} -output "+\n-\n-99\n"

MD(
B{read-number-expr} procedure

K{read-number-expr} reads numerical input, both integers and floating point
numbers. It actually takes in anything that starts out like a number and stops
at whitespace or an ending parenthesis or bracket, and then it accepts or
rejects the input by comparing it to a Tcl double. It returns a
R{Number}{numbers} object.
MD)

PR(
read-number-expr (internal);?char? tchr -> numeof
PR)

IX read-number-expr
CB(
proc ::constcl::read-number-expr {args} {
  upvar c c unget unget
  set unget {}
  if {[llength $args]} {
    lassign $args c
  } else {
    set c [readc]
  }
  read-eof $c
  while {[interspace $c] ne "#t" && $c ne "#EOF" &&
      $c ni {) ]}} {
    ::append num $c
    set c [readc]
  }
  set unget $c
  check {::string is double -strict $num} {
      Invalid numeric constant $num
  }
  set expr [N $num]
  return $expr
}
CB)

TT(
::tcltest::test read-15.0 {try reading a number} {
    set obj [::constcl::parse "99.99"]
    $obj value
} "99.99"

::tcltest::test read-15.1 {try reading a number} {
    set obj [::constcl::parse "     99.99"]
    $obj value
} "99.99"

::tcltest::test read-15.2 {try reading a number} {
    set obj [::constcl::parse "     9"]
    $obj value
} "9"

::tcltest::test read-15.3 {try reading a number} {
    set obj [::constcl::parse "     +9"]
    $obj value
} "9"

::tcltest::test read-15.4 {try reading a number} {
    set obj [::constcl::parse "     -9"]
    $obj value
} "-9"

::tcltest::test read-15.5 {try read-expr on numeric expr} -setup {
    ::tcltest::makeFile {  99 } testrr.lsp
    set p [pe {(open-input-file "testrr.lsp")}]
} -body {
    rw $p
} -cleanup {
    ::constcl::close-input-port $p
    ::tcltest::removeFile testrr.lsp
} -output "99\n"

TT)

MD(
B{read-unquoted-expr} procedure

When a comma is found in the input stream, K{read-unquoted-expr} is activated.
If it reads an at-sign (K{@}) it selects the symbol K{unquote-splicing}, otherwise
it selects the symbol K{unquote}. Then it reads an entire expression and returns
it wrapped in the selected symbol. Both of these expressions are only suppposed
to occur inside a quasiquoted expression.
MD)

PR(
read-unquoted-expr (internal);-> unquoteeof
PR)

IX read-unquoted-expr
CB(
proc ::constcl::read-unquoted-expr {} {
  upvar c c unget unget
  set unget {}
  set c [readc]
  read-eof $c
  if {$c eq "@"} {
    set symbol "unquote-splicing"
    set expr [read-expr]
  } else {
    set symbol "unquote"
    set expr [read-expr $c]
  }
  read-eof $expr
  return [list [S $symbol] $expr]
}
CB)

TT(

::tcltest::test read-16.0 {try reading unquoted symbol} -body {
    pw ",foo"
} -output "(unquote foo)\n"

::tcltest::test read-16.2 {try read-expr on unquoted expr} -setup {
    ::tcltest::makeFile {  ,foo ,@bar} testrr.lsp
    set p [pe {(open-input-file "testrr.lsp")}]
} -body {
    rw $p
    rw $p
} -cleanup {
    ::constcl::close-input-port $p
    ::tcltest::removeFile testrr.lsp
} -output "(unquote foo)\n(unquote-splicing bar)\n"

TT)

MD(
B{read-quasiquoted-expr} procedure

K{read-quasiquoted-expr} is activated when there is a backquote (K{`}) in the
input stream. It reads an entire expression and returns it wrapped in
K{quasiquote}.
MD)

PR(
read-quasiquoted-expr (internal);-> qquoteeof
PR)

IX read-quasiquoted-expr
CB(
proc ::constcl::read-quasiquoted-expr {} {
  upvar c c unget unget
  set unget {}
  set expr [read-expr]
  skip-ws
  read-eof $expr
  make-constant $expr
  return [list [S quasiquote] $expr]
}
CB)

TT(

::tcltest::test read-17.0 {try reading quasiquoted expression} -body {
    pw "`(list 1 2 ,@foo)"
} -output "(quasiquote (list 1 2 (unquote-splicing foo)))\n"

::tcltest::test read-17.1 {try read-expr on quasiquoted expr} -setup {
    ::tcltest::makeFile {  `(a b) } testrr.lsp
    set p [pe {(open-input-file "testrr.lsp")}]
} -body {
    rw $p
} -cleanup {
    ::constcl::close-input-port $p
    ::tcltest::removeFile testrr.lsp
} -output "(quasiquote (a b))\n"

TT)

MD(
B{read-identifier-expr} procedure

K{read-identifier-expr} is activated for 'anything else', and takes in
characters until it finds whitespace or an ending parenthesis or bracket. It
checks the input against the rules for identifiers, accepting or rejecting it
with an error message. It returns a R{Symbol}{symbols} object.
MD)

PR(
read-identifier-expr (internal);?chars? tchrs -> symeof
PR)

IX read-identifier-expr
CB(
proc ::constcl::read-identifier-expr {args} {
  upvar c c unget unget
  set unget {}
  if {[llength $args]} {
    set c [join $args {}]
  } else {
    set c [readc]
  }
  read-eof $c
  set name {}
  while {[::string is graph -strict $c]} {
    if {$c eq "#EOF" || $c in {) \]}} {
      break
    }
    ::append name $c
    set c [readc]
    # do not check for EOF here
  }
  if {$c ne "#EOF"} {
    set unget $c
  }
  read-eof $name
  # idcheck throws error if invalid identifier
  idcheck $name
  return [S $name]
}
CB)

TT(

::tcltest::test read-18.0 {try reading an identifier} {
    set expr [p "foo"]
    $expr name
} "foo"

::tcltest::test read-18.1 {try reading an identifier} -body {
    set expr [p "let"]
    ::constcl::varcheck [$expr name]
} -returnCodes error -result "Variable name is reserved: let"

::tcltest::test read-18.2 {try read-expr on identifiers} -setup {
    ::tcltest::makeFile {  foo    bar } testrr.lsp
    set p [pe {(open-input-file "testrr.lsp")}]
} -body {
    rw $p
    rw $p
} -cleanup {
    ::constcl::close-input-port $p
    ::tcltest::removeFile testrr.lsp
} -output "foo\nbar\n"

TT)

MD(
B{read-object-expr} procedure

A non-standard extension, K{read-object-expr} reads a ConsTcl object of any kind
and passes its name along.
MD)

PR(
read-object-expr (internal);-> objeof
PR)

IX read-object-expr
CB(
proc ::constcl::read-object-expr {} {
  upvar c c unget unget
  # first colon has already been read
  foreach ch [split ":oo::Obj" {}] {
    set c [readc]
    read-eof $c
    if {$c ne $ch} {
      error "bad object name"
    }
  }
  set res "::oo::Obj"
  set c [readc]
  read-eof $c
  while {[::string is digit $c]} {
    ::append res $c
    set c [readc]
    read-eof $c
  }
  set unget $c
  return $res
}
CB)

# vim: ft=tcl tw=80 ts=2 sw=2 sts=2 et 
