
H3 Booleans

MD(
Booleans are logic values, either true (K{#t}) or false (K{#f}).
All predicates (procedures whose name end with -?) return
boolean values. The conditional K{if} operator considers all
values except for K{#f} to be true.

B{Boolean} class
MD)

IX Boolean
CB(
oo::class create ::constcl::Boolean {
  superclass ::constcl::NIL
  variable boolval
  constructor {v} {
    if {$v ni {#t #f}} {
      ::error "bad boolean value $v"
    }
    set boolval $v
  }
  method mkconstant {} {}
  method constant {} {
    return 1
  }
  method boolval {} {
    set boolval
  }
  method value {} {
    set boolval
  }
  method write {handle} {
    puts -nonewline $handle [my boolval]
  }
  method display {handle} {
    my write $handle
  }
  method show {} {
    set boolval
  }
}
CB)

MD(
B{MkBoolean} generator

K{MkBoolean} generates a boolean. If a boolean with the same name already exists
(which is the case at run time, because there are only two valid boolean values,
and they're both pre-generated) that boolean will be returned, otherwise a fresh
boolean will be created.
MD)

PR(
MkBoolean (internal);bool ebool -> bool
PR)

IX MkBoolean
CB(
proc ::constcl::MkBoolean {bool} {
  foreach instance [info class instances \
    ::constcl::Boolean] {
    if {[$instance boolval] eq $bool} {
      return $instance
    }
  }
  return [::constcl::Boolean new $bool]
}
CB)

TT(

::tcltest::test boolean-1.0 {evaluate boolean values} -body {
        pew "#t"
} -output "#t\n"

::tcltest::test boolean-1.1 {evaluate boolean values} -body {
        pew "#f"
} -output "#f\n"

::tcltest::test boolean-1.2 {evaluate boolean values} -body {
        pew "'#f"
} -output "#f\n"

TT)

MD(
B{boolean?} procedure

The K{boolean?} predicate recognizes a Boolean by type.
MD)

PR(
boolean? (public);val val -> bool
PR)

IX boolean?
CB(
reg boolean? ::constcl::boolean?

proc ::constcl::boolean? {val} {
  return [typeof? $val Boolean]
}
CB)

TT(

::tcltest::test boolean-2.0 {evaluate boolean values} -body {
        pew "(boolean? #f)"
} -output "#t\n"

::tcltest::test boolean-2.1 {evaluate boolean values} -body {
        pew "(boolean? 0)"
} -output "#f\n"

::tcltest::test boolean-2.2 {evaluate boolean values} -body {
        pew "(boolean? '())"
} -output "#f\n"

TT)

MD(
B{not} procedure

The only operation on booleans: K{not}, or logical negation.
MD)

MD(
Example:
MD)

VB(
(not #f)    ⇒  #t   ; #f yields #t, all others #f
(not nil)   ⇒  #f   ; see?
VB)

PR(
not (public);val val -> bool
PR)

IX not
CB(
reg not ::constcl::not

proc ::constcl::not {val} {
  if {[$val boolval] eq "#f"} {
    return #t
  } else {
    return #f
  }
}
CB)

TT(

::tcltest::test boolean-3.0 {not procedure} -body {
        pew "(not #t)"
} -output "#f\n"

::tcltest::test boolean-3.1 {not procedure} -body {
        pew "(not #f)"
} -output "#t\n"

::tcltest::test boolean-3.2 {not procedure} -body {
        pew "(not (list 3))"
} -output "#f\n"

TT)

# vim: ft=tcl tw=80 ts=2 sw=2 sts=2 et 
